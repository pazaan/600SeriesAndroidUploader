import org.ajoberstar.grgit.Grgit
import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
        google()

        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.google.gms:google-services:4.3.4'
        classpath "io.realm:realm-gradle-plugin:10.0.1"
        classpath 'org.ajoberstar.grgit:grgit-core:4.1.0'
        classpath 'org.codehaus.groovy:groovy-json:3.0.6'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'realm-android'

repositories {
    maven { url "https://jitpack.io" }
}

// change dev to false for release version (release uses the generated version name and code)
def dev = false
def devName = "arecibo 0.7.3 (final)"

def generateVersionCode() {
    // We need to get the number of releases directly from GitHub, since Travis
    // does a "git clone --depth=50", meaning we don't get all the tags locally.
    String url = "https://api.github.com/repos/pazaan/600SeriesAndroidUploader/tags"
    def json = new JsonSlurper().parseText(url.toURL().text)

    def numOfTags = json.size()
    logger.warn("Generated Version Code: " + numOfTags)
    return numOfTags
}

def generateVersionName() {
    ext.repo = Grgit.open()

    def tag = ext.repo.describe().replaceFirst("^v", "")
    logger.warn("Generated Version Name: " + tag)
    return tag
}

def getBugfenderApiKey() {
    String bugfenderApiKey = System.getenv("BUGFENDER_API_KEY")

    if (bugfenderApiKey == null) {
        File file = new File("app/bugfender.properties")
        if (file.exists()) {
            Properties properties = new Properties()
            properties.load(new FileInputStream(file.getAbsolutePath().toString()))
            bugfenderApiKey = properties.getProperty("apiKey", "")
        } else {
            logger.warn("Bugfender API key not set")
            bugfenderApiKey = ""
        }
    }

    return "\"" + bugfenderApiKey + "\""
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "info.nightscout.android"
        minSdkVersion 16
        multiDexEnabled true
        targetSdkVersion 28
        versionName = dev ? devName : generateVersionName()
        versionCode = generateVersionCode()
        buildConfigField "String", "BUGFENDER_API_KEY", getBugfenderApiKey()
    }

    buildTypes {
        release {
            minifyEnabled false
            // Set to true once we set up proguard-rules without breaking anything
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        // Dag nabbit :(
        // Because of http://stackoverflow.com/questions/35492259/lint-error-on-okio
        warning 'InvalidPackage'
    }
}

release {
    tagTemplate = 'v${version}'
    buildTasks = ['build']
    beforeReleaseBuild.dependsOn 'clean'
}

dependencies {
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'

    def multidex_version = "2.0.1"
    implementation "androidx.multidex:multidex:$multidex_version"

    implementation platform('com.google.firebase:firebase-bom:26.1.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.bugfender.sdk:android:3.0.7'

    implementation files('libs/slf4j-api-1.7.2.jar')
    implementation files('libs/lzo-core-1.0.5.jar')

    implementation 'com.getpebble:pebblekit:4.0.1@aar'

    implementation 'com.github.thorbenprimke:realm-recyclerview:0.9.25'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'org.apache.commons:commons-lang3:3.4'

    implementation 'uk.co.chrisjenx:calligraphy:2.2.0'

    // due to bug in GraphView v4.2.1 using setNumHorizontalLabels reverted to using v4.0.1
    implementation 'com.jjoe64:graphview:4.0.1'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    // AppUpdater v2.7 uses com.squareup.okhttp3:okhttp:3.10.0
    implementation 'com.github.javiersantos:AppUpdater:2.7'

    // The version of okhttp3 *must* be the same as the version in AppUpdater
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    implementation('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }
    implementation 'com.mikepenz:google-material-typeface:3.0.1.1.original@aar'
    implementation 'com.mikepenz:ionicons-typeface:2.0.1.3@aar'
}

apply from: "signing.gradle"
