import org.ajoberstar.grgit.Grgit
import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
        classpath 'io.realm:realm-gradle-plugin:3.4.0'
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath 'org.codehaus.groovy:groovy-json:2.4.12'
    }
}
plugins {
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

def generateVersionCode() {
    // We need to get the number of releases directly from GitHub, since Travis
    // does a "git clone --depth=50", meaning we don't get all the tags locally.
    String url = "https://api.github.com/repos/pazaan/600SeriesAndroidUploader/tags"
    def json = new JsonSlurper().parseText(url.toURL().text)

    def numOfTags = json.size()
    logger.warn("Generated Version Code: " + numOfTags)
    return numOfTags
}

def generateVersionName() {
    ext.repo = Grgit.open()

    def tag = ext.repo.describe().replaceFirst("^v", "")
    logger.warn("Generated Version Name: " + tag)
    return tag
}

def getBugfenderApiKey() {
    String bugfenderApiKey = System.getenv("BUGFENDER_API_KEY")

    if(bugfenderApiKey == null) {
        File file = new File("app/bugfender.properties")
        if (file.exists()) {
            Properties properties = new Properties()
            properties.load(new FileInputStream(file.getAbsolutePath().toString()))
            bugfenderApiKey = properties.getProperty("apiKey", "")
        } else {
            logger.warn("Bugfender API key not set")
            bugfenderApiKey = ""
        }
    }

    return "\"" + bugfenderApiKey + "\""
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    // FIXME - replace with URLConnection. This is used in ManageCNLActivity.
    useLibrary 'org.apache.http.legacy'

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "info.nightscout.android"
        minSdkVersion 14
        targetSdkVersion 26
        versionName generateVersionName()
        versionCode generateVersionCode() // FIXME - this forces a need for an online lookup and compile fails when offline
        buildConfigField "String", "BUGFENDER_API_KEY", getBugfenderApiKey()
    }

    buildTypes {
        release {
            minifyEnabled false // Set to true once we set up proguard-rules without breaking anything
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            ext.enableCrashlytics = file("fabric.properties").exists()
        }

        debug {
            ext.enableCrashlytics = file("fabric.properties").exists()
        }
    }

    lintOptions {
        // Dag nabbit :(
        // Because of http://stackoverflow.com/questions/35492259/lint-error-on-okio
        warning 'InvalidPackage'
    }
}

release {
    tagTemplate = 'v${version}'
    buildTasks = ['build']
    beforeReleaseBuild.dependsOn 'clean'
}

dependencies {
    compile files('libs/slf4j-api-1.7.2.jar')
    compile files('libs/lzo-core-1.0.5.jar')

    compile 'com.getpebble:pebblekit:4.0.1@aar'

    compile 'com.android.support:percent:26.1.0'
    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.mikepenz:google-material-typeface:2.2.0.1.original@aar'
    compile 'com.mikepenz:ionicons-typeface:2.0.1.3@aar'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.bugfender.sdk:android:0.7.2'
    compile 'com.jjoe64:graphview:4.0.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    // The version of okhttp3 *must* be the same as the version in AppUpdater
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }
    compile('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }
    compile 'com.github.javiersantos:AppUpdater:2.6.1'
}

apply from: "signing.gradle"